dia {
  Main = Implementation+
  Implementation = ContainerImplementation | SynchronousLeafImplementation

ContainerImplementation = "implementation" name Box
SynchronousLeafImplementation 
  = "sync" name "<=" DatumList Box -- withFormals
  | "sync" name                Box -- withoutFormals

SynchronousCall 
  = "@" Datum "<=" Datum -- params
  | "@" Datum            -- noparams

Box = "{" BoxOperation Box* "}"
BoxOperation
  = ForEvery
  | Synonym
  | Find
  | WithLock
  | Cond
  | VarBox
  | SynchronousCall

Cond = "cond" CondClause+
CondClause = "{" Predicate Box "}"

WithLock = name "with" name "locked"
Find
  = "find" name "in" Datum "given" ParameterList "=>" name -- withParams
  | "find" name "in" Datum "=>" name                       -- withoutParams

VarBox = "var" name "<=" "{" DatumList "}"

Synonym = "synonym" name "=" DatumList
ForEvery
  = "for" "every" "item" "in" Datum "given" ParameterList "=>" name -- sugaredWithParams
  | "for" "every" name "in" Datum "given" ParameterList "=>" name -- withParams
  | "for" "every" "item" "in" Datum  "=>" name -- sugaredWithoutParams
  | "for" "every" name "in" Datum  "=>" name -- withoutParams



Datum
  = name "of" Datum -- field
  | name "." Datum  -- dottedField
  | name            -- solitary
  | "me" "of" Datum -- meField
  | "me" "." Datum  -- meDottedField
  | "me"            -- me

Predicate
  = Datum "==" Datum
  | Datum "!=" Datum

ParameterList
  = Datum "X" ParameterList -- list
  | Datum                   -- solitary

DatumList 
  = Datum "," DatumList -- list
  | Datum               -- solitary

name = namecharFirst namecharRest*
namecharFirst = ~separator ~keyword ("_" | "A" .. "Z" | "a" .. "z")
namecharRest = ~separator ~keyword ("_" | "A" .. "Z" | "a" .. "z" | "0" .. "9")

  separator
    = "<="
    | "=>"
    | "//"
    | "=="
    | "!="
    | "="
    | "{"
    | "}"
    | "("
    | ")"
    | "@"
    | "."
    | ","
    | eol

keyword
  = ("implementation"
  | "locked"
  | "given"
  | "every"
  | "find"
  | "cond"
  | "with"
  | "item"
  | "sync"
  | "for"
  | "in"
  | "of"
  | "me"
  | "X") 
  ~namecharRest

eol = "\n"

space
 += comment

comment
  = "//" (~"\n" any)* "\n"  -- singleLine
  | "/*" (~"*/" any)* "*/"  -- multiLine

}
