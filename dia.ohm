dia {
  Main = Implementation+
  Implementation = ContainerImplementation | SynchronousLeafImplementation

ContainerImplementation = "implementation" name Box
SynchronousLeafImplementation = SynchronousCall

SynchronousCall 
  = "@" Datum "<=" Datum -- params
  | "@" Datum           -- noparams

Box = "{" BoxOperation Box* "}"
BoxOperation
  = ForEvery
  | Synonym
  | Find
  | WithLock
  | Cond
  | SynchronousCall

Cond = "cond" CondClause+
CondClause = "{" Predicate Box "}"

WithLock = name "with" name "locked"
Find
  = "find" name "in" Datum "given" ParameterList "=>" name -- withParams
  | "find" name "in" Datum "=>" name                       -- withoutParams

Synonym = "synonym" name "=" DatumList
ForEvery
  = "for" "every" "item" "in" Datum "given" ParameterList "=>" name -- sugaredWithParams
  | "for" "every" name "in" Datum "given" ParameterList "=>" name -- withParams
  | "for" "every" "item" "in" Datum  "=>" name -- sugaredWithoutParams
  | "for" "every" name "in" Datum  "=>" name -- withoutParams



Datum
  = name "of" Datum -- field
  | name "." Datum  -- dottedfield
  | name            -- solitary
  | "me"            -- me

Predicate
  = Datum "==" Datum
  | Datum "!=" Datum

ParameterList
  = Datum "X" ParameterList -- list
  | Datum                   -- solitary

DatumList 
  = Datum "," DatumList -- list
  | Datum               -- solitary

name = namecharFirst namecharRest*
namecharFirst = ~separator ~keyword ("_" | "A" .. "Z" | "a" .. "z")
namecharRest = ~separator ~keyword ("_" | "A" .. "Z" | "a" .. "z" | "0" .. "9")

  separator
    = "<="
    | "=>"
    | "//"
    | "=="
    | "!="
    | "="
    | "{"
    | "}"
    | "("
    | ")"
    | "@"
    | "."
    | ","
    | eol

keyword
  = ("implementation"
  | "locked"
  | "given"
  | "every"
  | "find"
  | "cond"
  | "with"
  | "item"
  | "for"
  | "in"
  | "of"
  | "me"
  | "X") ~namecharRest

eol = "\n"

space
 += comment

comment
  = "//" (~"\n" any)* "\n"  -- singleLine
  | "/*" (~"*/" any)* "*/"  -- multiLine

}